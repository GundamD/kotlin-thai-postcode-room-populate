apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

ext {
    versionMajor = 1
    versionMinor = 0
    versionPatch = 0
    bintrayRepo = 'thai-postcode-room-populate'
    bintrayName = 'com.gundamd.thai_postcode_sdk'
    publishedGroupId = 'com.gundamd.thai_postcode_sdk'
    libraryName = 'thai-postcode-sdk'
    artifact = 'thai-postcode-sdk'
    libraryDescription = 'Library for search post code/zip code with address, implement by kotlin room populate.'
    siteUrl = 'https://github.com/GundamD/kotlin-thai-postcode-room-populate'
    gitUrl = 'https://github.com/GundamD/kotlin-thai-postcode-room-populate.git'
    libVersionCode = generateVersionCode()
    libraryVersion = generateVersionName(versionMajor, versionMinor, versionPatch, libVersionCode)
    developerId = 'gundamd'
    developerName = 'gundamd'
    developerEmail = 'apichainahom@gmail.com'
    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}


def static generateVersionCode() {
    return (System.currentTimeMillis() / 1000).toInteger()
}

def static generateVersionName(versionMajor, versionMinor, versionPatch, versionCode) {
    String versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
    return "${versionName}-${versionCode}"
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode libVersionCode
        versionName libraryVersion

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }

        // Database version that gets updated with import script
        buildConfigField "int", "ADDRESSES_DB_VERSION", ADDRESSES_DB_VERSION
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'

    // Room
    implementation 'androidx.room:room-runtime:2.2.5'
    kapt 'androidx.room:room-compiler:2.2.5'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

//Add these lines to publish library to bintray. This is the readymade scripts made by github user nuuneoi to make uploading to bintray easy.
//Place it at the end of the file
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'
